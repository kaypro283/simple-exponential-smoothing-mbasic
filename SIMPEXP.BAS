10 CLEAR
20 REM *** PROGRAM BY CHRISTOPHER D. VAN DER KAAY, PH.D. 2025 ***
30 REM *** SIMPLE EXPONENTIAL SMOOTHING FORECASTING TOOL (MBASIC-80) ***
40 REM *** REM - UP TO 100 DATA POINTS; FORECAST HORIZON N+H â‰¤ 100 ***
50 REM *** OPTIMIZES SMOOTHING PARAMETER ALPHA BY BRUTE-FORCE SEARCH ***
60 REM *** COMPUTES AND DISPLAYS STATISTICAL ACCURACY MEASURES ***
70 PRINT STRING$(79, "=")
80 PRINT TAB(20); "WELCOME TO THE TIME-SERIES FORECASTING TOOL"
90 PRINT STRING$(79, "=")
100 PRINT
110 PRINT TAB(10); "SIMPLE EXPONENTIAL SMOOTHING METHOD FOR TIME-SERIES FORECASTING"
120 PRINT TAB(10); STRING$(60, "-")
130 PRINT
140 PRINT TAB(5); "THIS PROGRAM USES THE SIMPLE EXPONENTIAL SMOOTHING METHOD"
150 PRINT TAB(5); "TO FORECAST FUTURE VALUES IN A TIME SERIES BASED ON HISTORICAL"
160 PRINT TAB(5); "DATA POINTS. IT CALCULATES FORECASTED VALUES BY AUTOMATICALLY"
170 PRINT TAB(5); "DETERMINING THE OPTIMAL ALPHA VALUE THAT MINIMIZES THE FORECAST ERROR"
180 PRINT TAB(5); "AND PROVIDES VARIOUS STATISTICAL MEASURES OF FORECAST ACCURACY."
190 PRINT TAB(5); "THIS METHOD IS USEFUL FOR SERIES WITH NO TREND OR SEASONAL PATTERN."
200 PRINT
210 PRINT STRING$(79, "-")
220 PROMPT$ = "LEARN ABOUT THE FORECAST ACCURACY METRICS BEFORE PROCEEDING? (Y/N): "
230 GOSUB 4110
240 IF RESP$ = "Y" THEN GOSUB 1920
250 PRINT STRING$(79, "-")
260 PRINT
270 PRINT "ENTER THE HISTORICAL DATA POINTS AND FORECAST HORIZON."
280 PRINT "THE PROGRAM WILL AUTOMATICALLY CALCULATE THE OPTIMAL ALPHA VALUE"
290 PRINT "THAT MINIMIZES THE FORECAST ERROR."
300 PRINT
310 PRINT STRING$(79, "=")
320 PRINT
330 DIM Y(100), F(100), S(100)   ' Y() = INPUT DATA; F() = FORECASTS; S() = SMOOTHED VALUES
340 DIM U(100), L(100)  ' U() AND L() FOR UPPER/LOWER CONFIDENCE INTERVALS; ALL ARRAYS INDEX 1..N+H
350 INPUT "ENTER NUMBER OF HISTORICAL DATA POINTS: "; N
360 IF N < 2 OR N > 100 THEN PRINT "INVALID INPUT. ENTER AN INTEGER BETWEEN 2 AND 100.": GOTO 350
370 AVE = 0
380 FOR I = 1 TO N
390 INPUT "ENTER DATA POINT "; A$   ' ACCEPT INPUT AS STRING TO VALIDATE NUMERICS
400 Y(I) = VAL(A$)   ' CONVERT TO NUMERIC VALUE
410 IF Y(I) = 0 AND A$ <> "0" THEN PRINT "INVALID INPUT. ENTER A NUMERICAL VALUE.": GOTO 390   ' ALLOW "0" ONLY IF INPUT IS STRING "0"
420 AVE = AVE + Y(I)
430 NEXT I
440 PRINT
450 GOSUB 4210 ' *** EXPLANATORY MESSAGE ABOUT STATIONARITY TEST ***
460 PROMPT$ = "VIEW THE FIRST-DIFFERENCED SERIES FOR STATIONARITY CHECK? (Y/N): "
470 GOSUB 4110
480 IF RESP$ = "Y" THEN GOSUB 3170
490 PROMPT$ = "COMPARE MEANS AND VARIANCES OF THE FIRST AND SECOND HALVES? (Y/N): "
500 GOSUB 4110
510 IF RESP$ = "Y" THEN GOSUB 3360
520 AVE = AVE / N
530 INPUT "ENTER FORECAST HORIZON: "; H
540 IF H <= 0 THEN PRINT "INVALID INPUT. ENTER A POSITIVE INTEGER.": GOTO 530
550 IF N + H > 100 THEN PRINT "N + H EXCEEDS 100. REDUCE FORECAST HORIZON.": GOTO 530
560 PRINT
570 INPUT "ENTER STEP SIZE (BETWEEN 0.001 AND 0.1): "; ALPHASTEP
580 IF ALPHASTEP <= 0 OR ALPHASTEP > .1 THEN PRINT "INVALID INPUT. ENTER A VALUE BETWEEN 0.001 AND 0.1.": GOTO 570
590 PRINT "CALCULATING OPTIMAL ALPHA VALUE..."
600 PRINT "USING STEP SIZE OF "; ALPHASTEP; " FOR ALPHA SEARCH..."
610 TSS = 0
620 FOR I = 1 TO N
630 TSS = TSS + (Y(I) - AVE) ^ 2
640 NEXT I
650 DIM TEMPS(100), TEMPF(100)
660 BALPHA = 0
670 MINMSE = 1E+30
680 FOR ALPHA = ALPHASTEP TO .99 STEP ALPHASTEP  ' BRUTE-FORCE GRID SEARCH FOR ALPHA IN [ALPHASTEP, 0.99]
690 TEMPS(1) = Y(1)
700 TEMPF(1) = Y(1)
710 SSE = 0
720 FOR I = 2 TO N   ' LOOP OVER DATA TO COMPUTE MSE FOR CURRENT ALPHA
730 TEMPS(I) = ALPHA * Y(I) + (1 - ALPHA) * TEMPS(I-1)
740 TEMPF(I) = TEMPS(I-1)
750 SSE = SSE + (Y(I) - TEMPF(I)) ^ 2
760 NEXT I
770 MSE = SSE / N
780 PRINT "ALPHA = "; ALPHA; " MSE = "; MSE  'PRINT ALPHA AND MSE
790 IF MSE < MINMSE THEN GOTO 810    ' KEEP MINIMUM MSE AND CORRESPONDING ALPHA
800 GOTO 830
810 MINMSE = MSE
820 BALPHA = ALPHA
830 NEXT ALPHA
840 ALPHA = BALPHA
850 PRINT CHR$(26):PRINT "OPTIMAL ALPHA VALUE IS: "; ALPHA
860 PRINT
870 INPUT "PRESS ANY KEY TO CONTINUE..."; DUMMY$
880 PRINT
890 S(1) = Y(1)
900 F(1) = Y(1)
910 SSE = 0
920 MAPE = 0
930 MAPECOUNT = 0
940 FOR I = 2 TO N
950 S(I) = ALPHA * Y(I) + (1 - ALPHA) * S(I-1)   ' COMPUTE SMOOTHED VALUE FOR PERIOD I
960 F(I) = S(I-1)   ' FORECAST FOR I IS PREVIOUS SMOOTHED VALUE
970 SSE = SSE + (Y(I) - F(I)) ^ 2
980 IF Y(I) <> 0 THEN GOTO 1000   ' ONLY CALCULATE MAPE IF ACTUAL DOES NOT EQUAL 0 TO AVOID DIVISION BY ZERO
990 GOTO 1020
1000 MAPE = MAPE + ABS((Y(I) - F(I)) / Y(I))
1010 MAPECOUNT = MAPECOUNT + 1
1020 NEXT I
1030 S(N+1) = ALPHA * Y(N) + (1 - ALPHA) * S(N)
1040 F(N+1) = S(N)
1050 FOR I = N+2 TO N+H
1060 S(I) = ALPHA * F(I-1) + (1 - ALPHA) * S(I-1)
1070 F(I) = S(I-1)
1080 NEXT I
1090 REM *** COMPUTE FORECAST INTERVALS ***
1100 GOSUB 3960
1110 SSE = SSE + (Y(N+1) - F(N+1)) ^ 2
1120 MSE = SSE / N
1130 MAE = 0
1140 COUNT = 0
1150 FOR I = 2 TO N
1160 MAE = MAE + ABS(Y(I) - F(I))
1170 COUNT = COUNT + 1
1180 NEXT I
1190 MAE = MAE / COUNT
1200 IF MAPECOUNT > 0 THEN MAPE = MAPE / MAPECOUNT * 100 ELSE MAPE = -1
1210 SMAPE = 0
1220 SMAPECOUNT = 0
1230 FOR I = 2 TO N
1240 NUM = ABS(Y(I) - F(I))
1250 DEN = ABS(Y(I)) + ABS(F(I))
1260 IF DEN = 0 THEN GOTO 1290   ' PREVENT DIVISION BY ZERO IF BOTH ACTUAL AND FORECAST ARE ZERO
1270 SMAPE = SMAPE + (NUM / (DEN / 2))
1280 SMAPECOUNT = SMAPECOUNT + 1
1290 NEXT I
1300 IF SMAPECOUNT > 0 THEN SMAPE = 100 * SMAPE / SMAPECOUNT ELSE SMAPE = -1
1310 PRINT CHR$(7):PRINT "FORECASTING RESULTS:"
1320 PRINT "============================================================"
1330 LINES = 0
1340 FOR I = 1 TO H
1350 PRINT "PERIOD " I;
1360 PRINT " : FORECAST = " F(N+I)
1370 PRINT "   LOWER 95% = " L(N+I)
1380 PRINT "   UPPER 95% = " U(N+I)
1390 PRINT
1400 LINES = LINES + 4
1410 IF LINES >= 15 THEN INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$: PRINT CHR$(26): LINES = 0   ' PAUSE AFTER 15 LINES
1420 NEXT I
1430 PRINT "============================================================"
1440 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
1450 PRINT
1460 PRINT "OPTIMAL ALPHA VALUE: "; ALPHA
1470 PRINT "MEAN ABSOLUTE ERROR (MAE): "; MAE
1480 PRINT "MEAN SQUARED ERROR (MSE): "; MSE
1490 PRINT "ROOT MEAN SQUARED ERROR (RMSE): "; (MSE ^ .5)
1500 PRINT "MEAN ABSOLUTE PERCENTAGE ERROR (MAPE): "; MAPE; "%"
1510 PRINT "SYMMETRIC MEAN ABSOLUTE PERCENTAGE ERROR (SMAPE): "; SMAPE; "%"
1520 PRINT
1530 REM *** DISPLAY MAPE THRESHOLDS AND INTERPRETATION ***
1540 PRINT "MAPE INTERPRETATION THRESHOLDS:"
1550 PRINT "-----------------------------------------"
1560 PRINT "MAPE < 10%           : HIGHLY ACCURATE FORECASTING"
1570 PRINT "10% <= MAPE < 20%    : GOOD FORECASTING"
1580 PRINT "20% <= MAPE < 50%    : REASONABLE FORECASTING"
1590 PRINT "MAPE >= 50%          : INACCURATE FORECASTING"
1600 PRINT "-----------------------------------------"
1610 PRINT
1620 REM *** INTERPRETATION OF MAPE ***
1630 IF MAPE <= 10 THEN GOTO 1700
1640 IF MAPE >= 10 AND MAPE <= 20 THEN GOTO 1690
1650 IF MAPE >= 20 AND MAPE < 50 THEN GOTO 1680
1660 IF MAPE >= 50 THEN GOTO 1670
1670 PRINT "INTERPRETATION: INACCURATE FORECASTING" : GOTO 1710
1680 PRINT "INTERPRETATION: REASONABLE FORECASTING" : GOTO 1710
1690 PRINT "INTERPRETATION: GOOD FORECASTING" : GOTO 1710
1700 PRINT "INTERPRETATION: HIGHLY ACCURATE FORECASTING"
1710 GOSUB 2050
1720 PRINT
1730 PRINT "(R)ESTART, (S)HOW RESULTS, (V)IEW PLOT, (Q) PRINT PLOT, (P)RINT REPORT, OR (E)ND"
1740 INPUT "(R/S/V/Q/P/E): "; R$
1750 IF R$ = "" THEN PRINT "ENTER R, S, V, Q, P, OR E.": GOTO 1730
1760 R$ = LEFT$(R$,1)
1770 IF R$ = "R" THEN R$ = "R"
1780 IF R$ = "S" THEN R$ = "S"
1790 IF R$ = "E" THEN R$ = "E"
1800 IF R$ = "V" THEN R$ = "V"
1810 IF R$ = "Q" THEN R$ = "Q"
1820 IF R$ = "P" THEN R$ = "P"
1830 IF R$ = "R" THEN GOTO 10
1840 IF R$ = "S" THEN GOTO 850
1850 IF R$ = "V" THEN GOSUB 2050 : GOTO 1730
1860 IF R$ = "Q" THEN GOSUB 4680 : GOTO 1730
1870 IF R$ = "P" THEN GOSUB 4420 : GOTO 1730
1880 IF R$ = "E" THEN GOTO 1900
1890 PRINT "INVALID INPUT. ENTER R, S, V, Q, P, OR E.": GOTO 1740
1900 PRINT CHR$(7):PRINT "THANK YOU FOR USING THE TIME-SERIES FORECASTING TOOL!"
1910 END
1920 REM *** EXPLANATION SUBROUTINE ***
1930 PRINT CHR$(26)
1940 PRINT STRING$(79, "=")
1950 PRINT TAB(22); "***** FORECAST ACCURACY METRICS *****"
1960 PRINT STRING$(79, "=")
1970 GOSUB 2420 ' MAE
1980 GOSUB 2580 ' MSE
1990 GOSUB 2730 ' RMSE
2000 GOSUB 2880 ' MAPE
2010 PRINT
2020 RETURN
2030 PRINT
2040 RETURN
2050 REM *** PLOT SUBROUTINE ***
2060 PROMPT$ = "PLOT THE DATA? (Y/N): "
2070 GOSUB 4110
2080 IF RESP$ <> "Y" THEN RETURN
2090 REM *** DETERMINE Y-AXIS RANGE ***
2100 MAXY = -1E+30
2110 MINY = 1E+30
2120 FOR I = 1 TO N
2130 IF Y(I) > MAXY THEN MAXY = Y(I)
2140 IF Y(I) < MINY THEN MINY = Y(I)
2150 NEXT I
2160 FOR I = 1 TO H
2170 IF F(N+I) > MAXY THEN MAXY = F(N+I)
2180 IF F(N+I) < MINY THEN MINY = F(N+I)
2190 NEXT I
2200 RANGEY = MAXY - MINY
2210 IF RANGEY = 0 THEN RANGEY = 1
2220 REM *** CALCULATE SCALING ***
2230 MAXDISP = 13
2240 TOTALPTS = N + H
2250 SCALEF = INT(TOTALPTS / MAXDISP)
2260 IF SCALEF < 1 THEN SCALEF = 1
2270 PRINT CHR$(26)
2280 PRINT "PERIOD"; TAB(12); "PLOT"
2290 PRINT STRING$(60, "-")
2300 FOR K = 1 TO TOTALPTS STEP SCALEF
2310 IF K <= N THEN GOTO 2350
2320 PLOTV = F(K)
2330 SYMBOL$ = "P"
2340 GOTO 2370
2350 PLOTV = Y(K)
2360 SYMBOL$ = "*"
2370 PLOTPOS = INT((PLOTV - MINY) / RANGEY * 45) + 12
2380 PRINT K; TAB(PLOTPOS); SYMBOL$
2390 NEXT K
2400 PRINT STRING$(60, "=")
2410 RETURN
2420 REM *** MAE (MEAN ABSOLUTE ERROR) EXPLANATION ***
2430 PRINT 
2440 PRINT " MAE (MEAN ABSOLUTE ERROR)"
2450 PRINT STRING$(79, "-")
2460 PRINT " THE MAE MEASURES THE AVERAGE MAGNITUDE OF THE"
2470 PRINT " ERRORS IN A SET OF FORECASTS, WITHOUT CONSIDERING"
2480 PRINT " THEIR DIRECTION." 
2490 PRINT
2500 PRINT " USAGE: MAE IS USED TO UNDERSTAND THE AVERAGE"
2510 PRINT " ERROR MAGNITUDE. LOWER MAE VALUES INDICATE BETTER"
2520 PRINT " ACCURACY."
2530 PRINT STRING$(79, "-")
2540 PRINT STRING$(79, "=")
2550 PRINT
2560 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
2570 RETURN
2580 REM *** MSE (MEAN SQUARED ERROR) EXPLANATION ***
2590 PRINT 
2600 PRINT " MSE (MEAN SQUARED ERROR)"
2610 PRINT STRING$(79, "-")
2620 PRINT " THE MSE MEASURES THE AVERAGE OF THE SQUARES OF"
2630 PRINT " THE ERRORS."
2640 PRINT
2650 PRINT " USAGE: MSE PENALIZES LARGER ERRORS MORE THAN"
2660 PRINT " SMALLER ONES, MAKING IT USEFUL FOR IDENTIFYING"
2670 PRINT " MODELS THAT NEED TO MINIMIZE LARGE ERRORS."
2680 PRINT STRING$(79, "-")
2690 PRINT STRING$(79, "=")
2700 PRINT
2710 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
2720 RETURN
2730 REM *** RMSE (ROOT MEAN SQUARED ERROR) EXPLANATION ***
2740 PRINT 
2750 PRINT " RMSE (ROOT MEAN SQUARED ERROR)"
2760 PRINT STRING$(79, "-")
2770 PRINT " THE RMSE IS THE SQUARE ROOT OF THE AVERAGE OF"
2780 PRINT " THE SQUARED ERRORS."
2790 PRINT
2800 PRINT " USAGE: RMSE PROVIDES A MEASURE OF THE SPREAD OF"
2810 PRINT " THE ERRORS IN THE SAME UNITS AS THE DATA, MAKING"
2820 PRINT " IT EASIER TO INTERPRET."
2830 PRINT STRING$(79, "-")
2840 PRINT STRING$(79, "=")
2850 PRINT
2860 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
2870 RETURN
2880 REM *** MAPE (MEAN ABSOLUTE PERCENTAGE ERROR) EXPLANATION ***
2890 PRINT 
2900 PRINT " MAPE (MEAN ABSOLUTE PERCENTAGE ERROR)"
2910 PRINT STRING$(79, "-")
2920 PRINT " THE MAPE MEASURES THE AVERAGE MAGNITUDE OF THE"
2930 PRINT " ERRORS AS A PERCENTAGE OF THE ACTUAL VALUES."
2940 PRINT
2950 PRINT " USAGE: MAPE NORMALIZES THE ERRORS AS PERCENTAGES,"
2960 PRINT " FACILITATING COMPARISONS ACROSS DIFFERENT SCALES."
2970 PRINT STRING$(79, "-")
2980 PRINT STRING$(79, "=")
2990 PRINT
3000 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
3010 REM *** SMAPE (SYMMETRIC MEAN ABSOLUTE PERCENTAGE ERROR) EXPLANATION ***
3020 PRINT 
3030 PRINT " SMAPE (SYMMETRIC MEAN ABSOLUTE PERCENTAGE ERROR)"
3040 PRINT STRING$(79, "-")
3050 PRINT " THE SMAPE IS A MODIFICATION OF MAPE THAT IS SYMMETRIC,"
3060 PRINT " MEANING IT TREATS OVER- AND UNDER-FORECASTS EQUALLY AND"
3070 PRINT " HANDLES ZERO OR NEAR-ZERO ACTUAL VALUES BETTER."
3080 PRINT
3090 RETURN
3100 PRINT " USAGE: SMAPE IS USEFUL FOR TIME SERIES WHERE ACTUALS"
3110 PRINT " CAN BE ZERO OR CLOSE TO ZERO. IT PROVIDES A PERCENTAGE"
3120 PRINT " ERROR THAT IS BOUNDED BETWEEN 0% AND 200%."
3130 PRINT STRING$(79, "-")
3140 PRINT STRING$(79, "=")
3150 PRINT
3160 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
3170 REM *** FIRST-DIFFERENCING SUBROUTINE WITH PAGING ***
3180 REM *** DISPLAYS THE FIRST-DIFFERENCED TIME SERIES (Y(I) - Y(I-1)), PAUSING EVERY 16 LINES ***
3190 PRINT CHR$(26)
3200 PRINT "FIRST-DIFFERENCED SERIES"
3210 PRINT STRING$(60, "-")
3220 LINES = 0
3230 FOR I = 2 TO N
3240 D = Y(I) - Y(I - 1)
3250 PRINT "DIFF("; I; ") = "; D
3260 LINES = LINES + 1
3270 IF LINES = 16 THEN GOSUB 3320: LINES = 0
3280 NEXT I
3290 PRINT STRING$(60, "-")
3300 IF LINES > 0 THEN GOSUB 3320
3310 RETURN
3320 REM *** PAUSE FOR PAGINATION ***
3330 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
3340 PRINT CHR$(26)
3350 RETURN
3360 REM *** MEAN AND VARIANCE STABILITY CHECK ***
3370 REM *** COMPUTE MIDPOINT OF SERIES ***
3380 MID = INT(N / 2)
3390 SUM1 = 0: SUM2 = 0
3400 FOR I = 1 TO MID
3410 SUM1 = SUM1 + Y(I)
3420 NEXT I
3430 FOR I = MID + 1 TO N
3440 SUM2 = SUM2 + Y(I)
3450 NEXT I
3460 MEAN1 = SUM1 / MID
3470 MEAN2 = SUM2 / (N - MID)
3480 REM *** NOW COMPUTE VARIANCES ***
3490 VAR1 = 0: VAR2 = 0
3500 FOR I = 1 TO MID
3510 VAR1 = VAR1 + (Y(I) - MEAN1) ^ 2
3520 NEXT I
3530 VAR1 = VAR1 / MID
3540 FOR I = MID + 1 TO N
3550 VAR2 = VAR2 + (Y(I) - MEAN2) ^ 2
3560 NEXT I
3570 VAR2 = VAR2 / (N - MID)
3580 REM *** PRINT RESULTS ***
3590 PRINT CHR$(26)
3600 PRINT "STATIONARITY CHECK: MEAN AND VARIANCE COMPARISON"
3610 PRINT STRING$(60, "-")
3620 PRINT "FIRST HALF MEAN     = "; MEAN1
3630 PRINT "SECOND HALF MEAN    = "; MEAN2
3640 PRINT "FIRST HALF VARIANCE = "; VAR1
3650 PRINT "SECOND HALF VARIANCE= "; VAR2
3660 PRINT STRING$(60, "-")
3670 IF ABS(MEAN2 - MEAN1) > .1 * ABS(MEAN1) THEN GOTO 3700   ' WARN IF MEAN SHIFT >10%
3680 IF VAR1 <> 0 THEN IF ABS(VAR2 - VAR1) > .2 * VAR1 THEN GOTO 3700   ' WARN IF VARIANCE SHIFT >20%
3690 GOTO 3710
3700 PRINT "WARNING: MEAN/VARIANCE SHIFT DETECTED! POSSIBLE NON-STATIONARITY!"
3710 PRINT
3720 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
3730 RETURN
3740 REM *** MAPE (MEAN ABSOLUTE PERCENTAGE ERROR) ***
3750 PRINT STRING$(79, "=")
3760 PRINT " MAPE (MEAN ABSOLUTE PERCENTAGE ERROR)"
3770 PRINT STRING$(79, "-")
3780 PRINT " THE MAPE MEASURES THE AVERAGE MAGNITUDE OF THE"
3790 PRINT " ERRORS AS A PERCENTAGE OF THE ACTUAL VALUES. IT"
3800 PRINT " IS CALCULATED AS:"
3810 PRINT
3820 PRINT "   MAPE = (100/N) * SUM(|(FORECASTI - ACTUALI) / ACTUALI|)"
3830 PRINT
3840 PRINT " WHERE:"
3850 PRINT "   N IS THE NUMBER OF FORECASTS"
3860 PRINT "   FORECASTI IS THE FORECASTED VALUE"
3870 PRINT "   ACTUALI IS THE ACTUAL VALUE"
3880 PRINT
3890 PRINT " USAGE: MAPE NORMALIZES THE ERRORS AS PERCENTAGES,"
3900 PRINT " FACILITATING COMPARISONS ACROSS DIFFERENT SCALES."
3910 PRINT STRING$(79, "-")
3920 PRINT STRING$(79, "=")
3930 PRINT
3940 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
3950 RETURN
3960 REM *** SUBROUTINE FOR COMPUTING FORECAST INTERVALS ***
3970 REM *** COMPUTE FORECAST INTERVALS ***
3980 RESVAR = SSE / N
3990 PHI = 1 - ALPHA
4000 FOR I = 1 TO H
4010 VARFACTOR = 0
4020 FOR J = 0 TO I - 1
4030 VARFACTOR = VARFACTOR + (PHI ^ (2 * J))
4040 NEXT J
4050 STDERR = SQR(RESVAR * VARFACTOR)
4060 ZVAL = 1.96  ' 95% CONFIDENCE
4070 U(N+I) = F(N+I) + ZVAL * STDERR
4080 L(N+I) = F(N+I) - ZVAL * STDERR
4090 NEXT I
4100 RETURN
4110 REM *** Y/N VALIDATION SUBROUTINE ***
4120 REM *** PROMPTS USER WITH PROMPT$, ACCEPTS ONLY Y/N (CASE-INSENSITIVE), REPEATS UNTIL VALID ***
4130 PRINT
4140 PRINT PROMPT$;
4150 INPUT ""; RESP$
4160 IF RESP$ = "" THEN PRINT "ENTER Y OR N.": GOTO 4140
4170 RESP$ = LEFT$(RESP$,1)
4180 IF RESP$ = "Y" OR RESP$ = "Y" THEN RESP$ = "Y": RETURN
4190 IF RESP$ = "N" OR RESP$ = "N" THEN RESP$ = "N": RETURN
4200 PRINT "INVALID INPUT. ENTER Y OR N.": GOTO 4140
4210 REM *** STATIONARITY CHECK EXPLANATION SUBROUTINE ***
4220 PRINT CHR$(26)
4230 PRINT STRING$(79, "=")
4240 PRINT "STATIONARITY CHECK EXPLANATION"
4250 PRINT STRING$(79, "-")
4260 PRINT "THE PROGRAM OPTIONALLY CHECKS FOR STATIONARITY BY COMPARING"
4270 PRINT "THE MEAN AND VARIANCE OF THE FIRST HALF AND SECOND HALF OF"
4280 PRINT "THE INPUT TIME SERIES."
4290 PRINT
4300 PRINT "A WARNING WILL BE ISSUED IF EITHER OF THE FOLLOWING IS TRUE:"
4310 PRINT " - THE ABSOLUTE DIFFERENCE IN MEANS EXCEEDS 10% OF THE"
4320 PRINT "   FIRST HALF'S MEAN."
4330 PRINT " - OR THE ABSOLUTE DIFFERENCE IN VARIANCES EXCEEDS 20% OF"
4340 PRINT "   THE FIRST HALF'S VARIANCE."
4350 PRINT
4360 PRINT "THIS INDICATES A SHIFT IN LEVEL OR SPREAD, SUGGESTING"
4370 PRINT "THE DATA MAY BE NON-STATIONARY, WHICH CAN REDUCE THE"
4380 PRINT "ACCURACY OF SIMPLE EXPONENTIAL SMOOTHING FORECASTS."
4390 PRINT STRING$(79, "-")
4400 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
4410 RETURN
4420 REM *** STATISTICAL REPORT ***
4430 LPRINT STRING$(79, "=")
4440 LPRINT TAB(22); "STATISTICAL SUMMARY REPORT"
4450 LPRINT STRING$(79, "=")
4460 LPRINT "INPUT DATA POINTS: "; N; TAB(36); "FORECAST HORIZON: "; H
4470 LPRINT
4480 LPRINT "OPTIMAL ALPHA: "; ALPHA
4490 LPRINT "MEAN ABSOLUTE ERROR (MAE): "; MAE
4500 LPRINT "MEAN SQUARED ERROR (MSE): "; MSE
4510 LPRINT "ROOT MEAN SQUARED ERROR (RMSE): "; (MSE ^ .5)
4520 IF MAPE >= 0 THEN LPRINT "MEAN ABSOLUTE PERCENTAGE ERROR (MAPE): "; MAPE; "%" : GOTO 4540
4530 LPRINT "MEAN ABSOLUTE PERCENTAGE ERROR (MAPE): N/A"
4540 IF SMAPE >= 0 THEN LPRINT "SYMMETRIC MAPE (SMAPE): "; SMAPE; "%" : GOTO 4560
4550 LPRINT "SYMMETRIC MAPE (SMAPE): N/A"
4560 LPRINT
4570 LPRINT "MAPE INTERPRETATION:"
4580 IF MAPE < 0 THEN LPRINT "   N/A" : GOTO 4630
4590 IF MAPE < 10 THEN LPRINT "   HIGHLY ACCURATE FORECASTING" : GOTO 4630
4600 IF MAPE < 20 THEN LPRINT "   GOOD FORECASTING" : GOTO 4630
4610 IF MAPE < 50 THEN LPRINT "   REASONABLE FORECASTING" : GOTO 4630
4620 LPRINT "   INACCURATE FORECASTING"
4630 LPRINT
4640 LPRINT STRING$(79, "=")
4650 LPRINT TAB(10); "END OF REPORT"
4660 LPRINT STRING$(79, "=")
4670 RETURN
4680 REM *** PRINT PLOT ***
4690 REM *** DETERMINE Y-AXIS RANGE ***
4700 LPRINT "*******************************************************"
4710 LPRINT "TIME-SERIES FORECAST OUTPUT CHART: OPTIMIZED SMOOTHING"
4720 LPRINT "OPTIMAL ALPHA: "; ALPHA
4730 LPRINT "*******************************************************"
4740 LPRINT
4750 MAXY = -1E+30
4760 MINY = 1E+30
4770 FOR I = 1 TO N
4780 IF Y(I) > MAXY THEN MAXY = Y(I)
4790 IF Y(I) < MINY THEN MINY = Y(I)
4800 NEXT I
4810 FOR I = 1 TO H
4820 IF F(N+I) > MAXY THEN MAXY = F(N+I)
4830 IF F(N+I) < MINY THEN MINY = F(N+I)
4840 NEXT I
4850 RANGEY = MAXY - MINY
4860 IF RANGEY = 0 THEN RANGEY = 1
4870 REM *** CALCULATE SCALING ***
4880 MAXDISP = 40
4890 TOTALPTS = N + H
4900 SCALEF = INT(TOTALPTS / MAXDISP)
4910 IF SCALEF < 1 THEN SCALEF = 1
4920 LPRINT "PERIOD"; TAB(12); "PLOT"
4930 LPRINT STRING$(68, "-")
4940 FOR K = 1 TO TOTALPTS STEP SCALEF
4950 IF K <= N THEN GOTO 4990
4960 PLOTV = F(K)
4970 SYMBOL$ = "P"
4980 GOTO 5010
4990 PLOTV = Y(K)
5000 SYMBOL$ = "*"
5010 PLOTPOS = INT((PLOTV - MINY) / RANGEY * 60) + 12
5020 LPRINT K; TAB(PLOTPOS); SYMBOL$
5030 NEXT K
5040 LPRINT STRING$(68, "=")
5050 LPRINT "     *** END OF REPORT ***"
5060 RETURN
